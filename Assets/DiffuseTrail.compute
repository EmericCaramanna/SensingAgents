// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel ProcessTrailMap

// Create a RenderTexture with enableRandomWrite flag and set it
// with cs.SetTexture
RWTexture2D<float4> TrailMap;
float evaporateSpeed;
float diffuseSpeed;
uint width;
uint height;
float deltaTime;

[numthreads(8,8,1)]
void ProcessTrailMap (uint3 id : SV_DispatchThreadID)
{
    if (id.x < 0 || id.x >= width || id.y < 0 || id.y >= height) {
        return;
    }

    float4 originalValue = TrailMap[id.xy];

    float4 sum = 0;
    for (int offsetx = -1; offsetx <= 1; offsetx++) {
        for (int offsety = -1; offsety <= 1; offsety++) {
            uint sampleX = id.x + offsetx;
            uint sampleY = id.y + offsety;
            if (sampleX < width && sampleY < height) {
                sum += TrailMap[int2(sampleX,sampleY)];
            }
        }
    }


    float4 blurResult = sum / 9;
    float4 diffusedValue = lerp(originalValue, blurResult, diffuseSpeed * deltaTime);
    float4 diffusedAndEvaporatedValue = max(0, diffusedValue - evaporateSpeed * deltaTime);

    TrailMap[id.xy] = diffusedAndEvaporatedValue;
}
